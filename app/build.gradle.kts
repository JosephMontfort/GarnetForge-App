import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
}

val keystoreProps = Properties().also { props ->
    val f = rootProject.file("keystore.properties")
    if (f.exists()) props.load(f.inputStream())
}

android {
    lint {
        checkReleaseBuilds = false
        abortOnError = false
    }
    namespace = "dev.garnetforge.app"
    compileSdk = 35

    defaultConfig {
        applicationId = "dev.garnetforge.app"
        minSdk = 31
        targetSdk = 35
        versionCode = 1
        versionName = "1.0.0"
    }

    signingConfigs {
        create("release") {
            storeFile   = keystoreProps.getProperty("KEYSTORE_PATH")?.let { file(it) }
            storePassword = keystoreProps.getProperty("KEYSTORE_PASS")
            keyAlias    = keystoreProps.getProperty("KEY_ALIAS")
            keyPassword = keystoreProps.getProperty("KEY_PASS")
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            isShrinkResources = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
            signingConfig = signingConfigs.getByName("release")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions { jvmTarget = "17" }
    buildFeatures { compose = true; buildConfig = true }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.material.icons)
    implementation(libs.androidx.navigation.compose)
    implementation(libs.libsu.core)
    implementation(libs.libsu.service)
    implementation(libs.coroutines.android)
    implementation(libs.datastore.preferences)
    debugImplementation(libs.androidx.ui.tooling)
}
